apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

version = '1.0-SNAPSHOT'
group = 'ca.redtoad'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    testCompile 'org.codehaus.groovy:groovy-all:2.4.13'
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

compileJava {
    options.compilerArgs << '-Xlint:all'
}

// create javadoc and sources jars
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

javadoc {
    options.links << 'https://docs.oracle.com/javase/8/docs/api'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// sign archives
signing {
    sign configurations.archives
}

// deployment to osssh
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Java Stream Extras'
                packaging 'jar'
                description 'Extras for Java 8+ streams.'
                url 'https://github.com/ataylor284/javastream-extras'

                scm {
                    connection 'scm:git:git://github.com/ataylor284/javastream-extras.git'
                    developerConnection 'scm:git:git@github.com:ataylor284/javastream-extras.git'
                    url 'https://github.com/ataylor284/javastream-extras'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'ataylor'
                        name 'Andrew Taylor'
                        email 'ataylor@redtoad.ca'
                    }
                }
            }
        }
    }
}
